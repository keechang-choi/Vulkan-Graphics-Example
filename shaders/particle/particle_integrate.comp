
#version 450

struct Particle
{
	vec4 pos;
	vec4 vel;
	vec4 pk[4];
	vec4 vk[4];
	vec4 attractionIndex;
	vec4 attractionWeight;
};

// Binding 0 : Position storage buffer prev frame
layout(std430, binding = 0) readonly buffer SSBOIn
{
   Particle particlesIn[ ];
};

// Binding 1 : Position storage buffer
layout(std430, binding = 1) buffer SSBO
{
   Particle particles[ ];
};

layout (binding = 2) uniform UBO 
{
	vec4 clickData;
	float dt;
	int particleCount;
	float gravity;
	float power;
	float soften;
} ubo;

layout (constant_id = 1) const int INTEGRATOR = 0;
layout (constant_id = 2) const int INTEGRATE_STEP = 0;
layout (local_size_x_id = 3) in;

void main() 
{
	// Current SSBO index
	uint index = gl_GlobalInvocationID.x;
	if (index >= ubo.particleCount) 
		return;	

	if (INTEGRATOR == 1){
		// euler method
		particles[index].pos.xyz = particlesIn[index].pos.xyz + particles[index].pk[0].xyz;
		particles[index].vel.xyz = particlesIn[index].vel.xyz + particles[index].vk[0].xyz;
	} else if(INTEGRATOR == 2){
		// midpoint method
		particles[index].pos.xyz = particlesIn[index].pos.xyz + particles[index].pk[1].xyz;
		particles[index].vel.xyz = particlesIn[index].vel.xyz + particles[index].vk[1].xyz;
	} else if(INTEGRATOR == 4){
		// RK4 integrate
		particles[index].pos.xyz = particlesIn[index].pos.xyz + (
			particles[index].pk[0].xyz + 
			2.0 * particles[index].pk[1].xyz + 
			2.0 * particles[index].pk[2].xyz + 
			particles[index].pk[3].xyz
		) / 6.0;

		particles[index].vel.xyz = particlesIn[index].vel.xyz + (
			particles[index].vk[0].xyz + 
			2.0 * particles[index].vk[1].xyz + 
			2.0 * particles[index].vk[2].xyz + 
			particles[index].vk[3].xyz
		) / 6.0;
	}else  if(INTEGRATOR == 5){
		// euler symplecitc method
		particles[index].vel.xyz = particlesIn[index].vel.xyz + particles[index].vk[0].xyz;
		particles[index].pos.xyz = particlesIn[index].pos.xyz + ubo.dt * particles[index].vel.xyz;
	}else  if(INTEGRATOR == 6){
		// verlet method
		particles[index].pos.xyz = particles[index].pk[1].xyz;
		particles[index].vel.xyz = particles[index].vk[1].xyz; 
	}else if(INTEGRATOR == 8){
		// ruth or yoshida, 4th-order symplectic
		particles[index].pos.xyz = particles[index].pk[3].xyz;
		particles[index].vel.xyz = particles[index].vk[3].xyz;
	}
}