
#version 450

struct Particle
{
	vec4 pos;
	vec4 vel;
	vec4 pk[4];
	vec4 vk[4];
};

// Binding 0 : Position storage buffer prev frame
layout(std140, binding = 0) readonly buffer SSBOIn
{
   Particle particlesIn[ ];
};

// Binding 1 : Position storage buffer
layout(std140, binding = 1) buffer SSBO
{
   Particle particles[ ];
};

layout (local_size_x = 256) in;

layout (binding = 2) uniform UBO 
{
	float dt;
	int particleCount;
} ubo;

layout (constant_id = 4) const int RKSTEP = 0;

void main() 
{
	// Current SSBO index
	uint index = gl_GlobalInvocationID.x;
	if (index >= ubo.particleCount) 
		return;	

	if (RKSTEP == 1){
		// euler method
		particles[index].pos.xyz = particlesIn[index].pos.xyz + particles[index].pk[0].xyz;
		particles[index].vel.xyz = particlesIn[index].vel.xyz + particles[index].vk[0].xyz;
	} else if(RKSTEP == 2){
		// midpoint method
		particles[index].pos.xyz = particlesIn[index].pos.xyz + particles[index].pk[1].xyz;
		particles[index].vel.xyz = particlesIn[index].vel.xyz + particles[index].vk[1].xyz;
	} else if(RKSTEP == 4){
		// RK4 integrate
	particles[index].pos.xyz = particlesIn[index].pos.xyz + (
		particles[index].pk[0].xyz + 
		2.0 * particles[index].pk[1].xyz + 
		2.0 * particles[index].pk[2].xyz + 
		particles[index].pk[3].xyz
	) / 6.0;

	particles[index].vel.xyz = particlesIn[index].vel.xyz + (
		particles[index].vk[0].xyz + 
		2.0 * particles[index].vk[1].xyz + 
		2.0 * particles[index].vk[2].xyz + 
		particles[index].vk[3].xyz
	) / 6.0;
	}


}